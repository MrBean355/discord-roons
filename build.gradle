plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'org.jetbrains.kotlin.kapt' version '1.3.50'
}

group 'com.github.mrbean355'
version '1.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'io.ktor:ktor-server-netty:1.2.4'
    implementation 'io.ktor:ktor-gson:1.2.4'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.discord4j:discord4j-core:3.0.8'
    implementation 'com.discord4j:discord4j-voice:3.0.8'
    implementation 'com.sedmelluq:lavaplayer:1.3.20'
    implementation "com.google.dagger:dagger:2.24"
    kapt "com.google.dagger:dagger-compiler:2.24"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

mainClassName = 'com.github.mrbean355.roons.MainKt'

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

task copySound(type: Copy) {
    from 'src/main/resources/roons.mp3'
    into "${buildDir.path}/libs"
}

jar.finalizedBy(copySound)